apply plugin: 'application'
apply plugin: 'com.bmuschko.docker-java-application'
// apply the shadow plugin
apply plugin: 'com.github.johnrengelman.shadow'

ext {
    flinkVersion = '1.17.0'
    flinkBinaryVersion = '1.17'
    hudiVersion = '0.14.0'
}

mainClassName = "com.hudi.flink.quickstart.HudiDataStreamWriter"

dependencies {
    implementation "org.apache.flink:flink-streaming-java:${flinkVersion}"
    implementation "org.apache.flink:flink-clients:${flinkVersion}"
    implementation "org.apache.flink:flink-table-common:${flinkVersion}"
    implementation "org.apache.flink:flink-table-runtime:${flinkVersion}"
    implementation "org.apache.hudi:hudi-flink${flinkBinaryVersion}-bundle:${hudiVersion}"
    implementation 'org.apache.hadoop:hadoop-common:2.10.1'
    implementation 'org.apache.hadoop:hadoop-hdfs:2.10.1'
    implementation 'org.apache.hadoop:hadoop-aws:2.10.1'
    implementation 'com.amazonaws:aws-java-sdk-bundle:1.11.18'
    implementation 'org.apache.flink:flink-s3-fs-hadoop:1.17.2' // Replace ${flinkVersion} with your Flink version
}

// create the shadowJar task
jar.enabled = false
shadowJar {
    archiveClassifier.set('')
    zip64 true
}

def installDir = file("${buildDir}/libs")
def ci = System.getenv('GITHUB_ACTIONS')
def imageRepository = ci ? 'hudi' : 'localhost:5001/hudi'
docker {
    dockerSyncBuildContext {
        from installDir
    }

    dockerCreateDockerfile {
        instruction 'WORKDIR /opt/hudi/examples'
        instruction 'COPY example.jar streaming/'
    }

    javaApplication {
        baseImage = 'flink:1.17'
        maintainer = 'Sundaram Ananthanarayanan "me@sundaram.io"'
        mainClassName = 'com.hudi.flink.quickstart.HudiDataStreamWriter'
        images = ["$imageRepository/hudi-flink:latest"]
    }
}
dockerSyncBuildContext.dependsOn(shadowJar)

// task to create a pod with the above image
def podFile = file('pod.yml')
task createPod(type: Exec) {
    commandLine 'kubectl', 'create', '-f', podFile.toPath().toAbsolutePath().toString()
}

// task to delete the pod created with the above task
task deletePod(type: Exec) {
    commandLine 'kubectl', 'delete', '-f', podFile.toPath().toAbsolutePath().toString()
}

// set up port forwarding to the pod
task openFlinkUI(type: Exec) {
    commandLine 'kubectl', 'port-forward', 'svc/basic-example-rest', '8081'
}
